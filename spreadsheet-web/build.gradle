buildscript {
    ext {
        /** Spring **/
        spring_boot_version = '2.4.5'
        spring_version = '5.3.6'
        moowork_version = '1.3.1'
        node_version = '12.13.1'
        npm_version = '6.14.11'
        sheet_version = '1.0.0'
    }

    repositories {
        maven { url "https://jitpack.io" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
        mavenCentral()
    }
}

plugins {
    id 'org.springframework.boot' version "${spring_boot_version}"
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.moowork.node' version "${moowork_version}"
    id 'java'
    id 'war'
    id 'jacoco'
}

group = 'com.js'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_8

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.oracle.database.jdbc:ojdbc8'
    runtimeOnly 'mysql:mysql-connector-java'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    /* model mapper */
    implementation group: 'org.modelmapper', name: 'modelmapper', version: '2.4.0'


}


/**
 * Include ui build task in build process
 */
node{
    version = "${node_version}"
    npmVersion = "${npm_version}"
    workDir = file("${projectDir.path}/src/front-end")
    npmWorkDir = file("${projectDir.path}/src/front-end")
    nodeModulesDir = file("${projectDir.path}/src/front-end")
}

task setUp(type: NpmTask){
    description = " Install Node.js packages"
    args = ['install']
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task buildFrontEnd(type: NpmTask, dependsOn: setUp){
    description = "Build vue js"
    args = ['run', 'build']
}

processResources.dependsOn 'buildFrontEnd'

test {
    useJUnitPlatform()
    //TODO: something
    println project.gradle.startParameter.taskNames
    if (project.gradle.startParameter.taskNames.find{ it.endsWith("test") }){
        setUp.enabled = false
        buildFrontEnd.enabled = false
    }
    finalizedBy 'jacocoTestReport'
}

bootWar {
    archiveFileName = "SpreadSheet-v${sheet_version}.war"
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = false
        csv.enabled = false
        html.destination file("${buildDir}/reports/")
    }
    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }

            excludes = ['com.js.spreadsheet.SpreadsheetApplication']
        }
    }
}
